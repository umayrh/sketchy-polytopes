version = "0.1"
description = "Apache Spark-Scala Projects"

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'cz.alenkacz:gradle-scalafmt:1.6.0'
    classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3+'
  }
}

subprojects {
  repositories {
    mavenCentral()
    jcenter()
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
  }

  apply plugin: 'scala'
  apply plugin: 'scalafmt'
  apply plugin: 'com.github.johnrengelman.shadow'

  dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion + ".+"

    compile group: 'org.apache.spark', name: "spark-core_"     + scalaVersion, version: sparkVersion
    compile group: 'org.apache.spark', name: "spark-sql_"      + scalaVersion, version: sparkVersion
    compile group: 'org.apache.spark', name: "spark-mllib_"    + scalaVersion, version: sparkVersion
    compile group: 'org.apache.spark', name: "spark-graphx_"   + scalaVersion, version: sparkVersion
    compile group: 'org.apache.spark', name: "spark-streaming_"+ scalaVersion, version: sparkVersion
    compile group: 'org.apache.spark', name: "spark-hive_"     + scalaVersion, version: sparkVersion
    compile group: 'org.apache.spark', name: "spark-catalyst_" + scalaVersion, version: sparkVersion
    compile group: 'com.esotericsoftware', name: "kryo",                       version: '4.0.2'
    compile group: 'com.google.guava', name: "guava",                          version: '25.0-jre'
    compile group: 'me.lemire.integercompression', name: "JavaFastPFOR",       version: '0.1.+'

    testCompile group: 'org.scalatest', name: "scalatest_" + scalaVersion, version: '3.+'
    testCompile group: 'org.scalacheck', name: "scalacheck_" + scalaVersion, version: '1.+'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'com.holdenkarau', name: 'spark-testing-base_' + scalaVersion, version: sparkVersion + "_0.+"
  }

  shadowJar {
    // See https://github.com/johnrengelman/shadow/issues/107
    // This can produce is really fat JAR (5k -> 188M), so might
    // want to prune some libs here, maybe Spark ones.
    // Alternatively, the Spark libs may be set to 'compileOnly'
    // instead of 'compile'.
    zip64 true
  }

  task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', 'build/classes/scala/test', '-o']
    classpath = sourceSets.test.runtimeClasspath
  }

  compileScala.dependsOn scalafmtAll
  test.dependsOn scalaTest
}

