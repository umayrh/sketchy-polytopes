version = "0.1"
description = "Apache Spark-Scala Projects"

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'cz.alenkacz:gradle-scalafmt:1.6.0'
    classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3+'
  }
}

subprojects {
  repositories {
    mavenCentral()
    jcenter()
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
  }

  apply plugin: 'scala'
  apply plugin: 'scalafmt'
  apply plugin: 'com.github.johnrengelman.shadow'

  dependencies {
    compileOnly group: 'org.scala-lang', name: 'scala-library', version: scalaVersion + ".+"

    // Setting Spark libs to be compileOnly since otherwise the shadow JAR
    // might end up being very fat (5K -> 188M vs. 5K -> 2.7M).
    compileOnly group: 'org.apache.spark', name: "spark-core_"     + scalaVersion, version: sparkVersion
    compileOnly group: 'org.apache.spark', name: "spark-sql_"      + scalaVersion, version: sparkVersion
    compileOnly group: 'org.apache.spark', name: "spark-mllib_"    + scalaVersion, version: sparkVersion
    compileOnly group: 'org.apache.spark', name: "spark-graphx_"   + scalaVersion, version: sparkVersion
    compileOnly group: 'org.apache.spark', name: "spark-streaming_"+ scalaVersion, version: sparkVersion
    compileOnly group: 'org.apache.spark', name: "spark-hive_"     + scalaVersion, version: sparkVersion
    compileOnly group: 'org.apache.spark', name: "spark-catalyst_" + scalaVersion, version: sparkVersion

    testCompile group: 'org.scalatest', name: "scalatest_" + scalaVersion, version: '3.+'
    testCompile group: 'org.scalacheck', name: "scalacheck_" + scalaVersion, version: '1.+'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'com.holdenkarau', name: 'spark-testing-base_' + scalaVersion, version: sparkVersion + "_0.+"
  }

  // Give test dependencies access to compileOnly dependencies to emulate providedCompile.
  // Otherwise, tests won't compile.
  // https://discuss.gradle.org/t/compileonly-dependencies-are-not-available-in-tests/15366/7
  configurations {
    testImplementation.extendsFrom compileOnly
  }

  shadowJar {
    // See https://github.com/johnrengelman/shadow/issues/107
    zip64 true
  }

  task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', 'build/classes/scala/test', '-o']
    classpath = sourceSets.test.runtimeClasspath
  }

  compileScala.dependsOn scalafmtAll
  test.dependsOn scalaTest
}

